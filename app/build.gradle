plugins {
    alias(libs.plugins.android.app)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.hilt)
    alias(libs.plugins.navigation.safeargs)
    alias(libs.plugins.gms)
}

android {
    namespace 'app.vazovsky.healsted'
    compileSdk 33

    defaultConfig {
       // manifestPlaceholders = [hostName:"healsted.vazovsky.app"]
        applicationId "app.vazovsky.healsted"
        minSdk 24
        targetSdk 33
        versionCode 2
        versionName "1.0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    packagingOptions {
        exclude "META-INF/gradle/incremental.annotation.processors"
    }
    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            versionNameSuffix "-release"
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
        debug{
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            minifyEnabled false
            signingConfig signingConfigs.debug
            debuggable true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation(libs.material)
    implementation(libs.core)
    implementation(libs.androidx.navigation.fragment)
    implementation(libs.androidx.navigation.ui)
    implementation(libs.androidx.splashscreen)
    implementation(libs.androidx.work)

    implementation(libs.kotlin)
    implementation(libs.kotlin.serialization)
    implementation(libs.kotlin.datetime)
    coreLibraryDesugaring(libs.desugaring)

    implementation(libs.hilt)
    kapt(libs.hilt.compiler)
    implementation(libs.hilt.work)
    kapt(libs.hilt.work.compiler)

    implementation(libs.view.binding)
    implementation(libs.timber)

    implementation(libs.retrofit)
    implementation(libs.retrofit.gson)

    debugImplementation(libs.chucker)
    releaseImplementation(libs.chucker.release)

    implementation(libs.lottie)
    implementation(libs.glide)
    annotationProcessor(libs.glide.compiler)

    implementation(platform(libs.firebase))
    implementation(libs.firebase.auth)
    implementation(libs.firebase.analytics)
    implementation(libs.firebase.firestore)

    implementation(libs.bubble.tab.bar)
    implementation(libs.decoro)

    implementation(libs.room.runtime)
    implementation(libs.room.ktx)
    kapt(libs.room.compiler)
}